//https://www.shadertoy.com/view/MlGXDm
//shader Lava

#version 330
uniform float iTime;
uniform vec2 iResolution;
uniform vec2 iMouse;
out vec4 fragColor;

// LAVE ---------------------------------------------------------------

// Hashing function, use sin instead of table with permutations
// n : Real value
float hash( float n ) 
{
    return fract(sin(n)*43758.5453123); 
}

// Noise
// x : Point in space
float noise( in vec3 x )
{
    vec3 p = floor(x);
    vec3 f = fract(x);
    f = f*f*(3.0-2.0*f);
	
    float n = p.x + p.y*157.0 + 113.0*p.z;
    return mix(mix(mix( hash(n+  0.0), hash(n+  1.0),f.x),
                   mix( hash(n+157.0), hash(n+158.0),f.x),f.y),
               mix(mix( hash(n+113.0), hash(n+114.0),f.x),
                   mix( hash(n+270.0), hash(n+271.0),f.x),f.y),f.z);
}

// Rotation ------------------------------------------------------------

// Rotation around y axis
// v : Vector
// a : Angle
vec3 rotate(in vec3 v,in float a)
{
    return vec3(v.x*cos(a)+v.z*sin(a),v.y,-v.x*sin(a)+v.z*cos(a));
}

// Textures ------------------------------------------------------------

const vec3 lave=vec3(0.99,0.0,0.0);
const vec3 lavedure=vec3(0.29,0.0,0.0);
const vec3 brown=vec3(0.2,0.1,0.0);

// Checker
// p : Point on object
// n : Normal at point
vec3 checker(in vec3 p,in vec3 n)
{
    float v=mod(floor(p.x)+floor(p.y)+floor(p.z),2.0);
    return mix(lave,brown,v);
}

// Turbulence
// p : Point
// n : Normal
float turbulence(in vec3 p,in vec3 n)
{
    // Add scaled noises
    float t=noise(p);
    t+=0.25*noise(2.0*p);
    t+=0.125*noise(5.0*p);
    t+=0.025*noise(13.0*p);
    // Scale
    t/=(1.0+0.25+0.125+0.025);
    return t;
}

// Turbulence with color
vec3 scaled(in vec3 p,in vec3 n)
{
    // Add scaled noises
    float t=turbulence(p,n);

    return mix(lave,brown,t);
}

// Lave
// p : Point
// n : Normal
vec3 lava(in vec3 p,in vec3 n)
{
    // Add scaled noises
    float t=turbulence (10.0*p,n);
   
    if(t<0.5)
    {
        vec3 red = vec3(0.25,0.0,0.0);
        float u=turbulence(25.0*p+iTime*1.0,n);
        
        // w= 1 sur le bord, 0 dans la lave
        float w=2.0*t;
        // w= 1 dans la lave 0 bord
        w=1.0-w;
        
    	vec3 lave=mix(lave,red,u);
        lave=mix(lave,lavedure,w);
        return lave;
        
    }

    
        vec3 black = vec3(0.,0.0,0.0);
        t=turbulence(35.0*p,n);
    	return mix(black,brown,t);

}

// Warped checker
// p : Point
// n : Normal
// a : amount of warping
vec3 warped(in vec3 p,in vec3 n,in float a)
{
    return checker(p+a*noise(2.0*p),n);
}

// Objects --------------------------------------------------------------

// Intersection between a ray and a sphere
// o : Ray origin
// d : Ray direction
// c : Center of sphere
// r : Radius
// t : Intersection depth
// n : Normal at intersection point
bool sphere(in vec3 o,in vec3 d,in vec3 c,in float r,out float t,out vec3 n)
{
	vec3 oc = o-c;
    
    float b=dot(d,oc);
    float k = dot(oc,oc)-r*r;
    t=b*b-k;
       
    if (t<=0.0) return false;
    
    t=-b-sqrt(t);
    if (t<0.0) return false;
    
    // Normal
    n=(o+t*d-c)/r;

   return true;
 }

// Lighting -------------------------------------------------------------

// Background color
// r : Ray direction
vec3 background(in vec3 r)
{
    return mix(vec3(0.2, 0.3, 0.4), vec3(0.7, 0.8, 1.0), r.y*0.5+0.5);
}

// p : Point on object
// n : normal at point
vec3 shade(in vec3 p,in vec3 n)
{
    // Point light
    const vec3 lightPos = vec3(1.0, 1.0,-5.0);
    const vec3 lightColor = vec3(0.95, 0.95,0.95);
  
    vec3 l = normalize(lightPos - p);

    // Not even Phong shading, use weighted cosine instead for smooth transitions
    float diff = 0.5*(1.0+dot(n, l));

    // Change call to Texture there : apply either color() or sine() or checker() or whatever texture you wish
    vec3 c = lava(p,n)*diff*lightColor;

    return c;
}

// Vignetting	
// c : Color
// p : Point in screen space
vec4 Vignetting(in vec4 c,in vec2 p)
{
	return c * ( 0.5 + 0.5*pow( (p.x+1.0)*(p.y+1.0)*(p.x-1.0)*(p.y-1.0), 0.1 ) );
}

// Main -----------------------------------------------------------------

void main( void ) {
	 vec2  fragCoord = gl_FragCoord.xy;
    vec2 xy = -1.0 + 2.0*fragCoord.xy/iResolution.xy;
    vec2 uv = xy * vec2(iResolution.x/iResolution.y, 1.0);
    
	// Ray origin and direction
    vec3 o = vec3(0.0, 0.0, -2.0);
	vec3 d = normalize(vec3(uv, 1.0));
	
    o=rotate(o,0.25*iTime);
    d=rotate(d,0.25*iTime);
    
	vec3 n;
    float t;
    
    // Default background color
    fragColor=vec4(background(d),1.0);
    if (sphere(o, d, vec3(0.0, 0.0, 0.0), 1.5, t, n))
    {	
        fragColor = vec4( mix(background(d), shade(o+d*t,n), step(0.0, t)), 1.0 );
    }
    fragColor=Vignetting(fragColor,xy);
}
